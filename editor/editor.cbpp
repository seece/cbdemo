// sync editor
// hotkeys: HOLD CTRL	move all nodes to the right
// 			HOLD ALT	disable snapping
//			HOLD X		click to remove nodes
//			DEL			when holding node, deletes node
//			RIGHT CLICK	add node
//			F6			save current track
//			F9			load current track

// config

 #define STANDALONE_EDITOR		// compile with main loop etc.
 #define USE_EDITOR_COMPONENTS	// if this is disabled, only compiles the parts needed for playing back data

// macros

#define malloc(size) makeMEMBlock(size)
#define free deleteMEMBlock
#define realloc resizeMEMBlock
#define toType(integer) convertToType(integer)
#define toInt(typevar) convertToInteger(typevar)
#define assert(statement) If Not (statement) Then makeError __LINE__ + ": assertion failed"

#define frac(_var) (_var-roundDown(_var))
#define round(_val) ()
#define lerp(_val1, _val2, _amount) (_val1 + (_val2 - _val1) * _amount)
#define barsToSecs(_bars, _tempo) (_bars * ((60.0/_tempo)*4.0))
#define snapToGrid(_value, _size) (int(_value/_size)*_size)
#define clamp(_val) (max(0.0,min(1.0, _val)))

#define POINT_STRIDE 8
#define UNDEFINED -1

#define UI_POINT_SIZE 8
#define UI_COLOR_DARK 24, 24, 30
#define UI_COLOR_BG 50, 50,64
#define UI_COLOR_DIM 80,80,80
#define UI_COLOR_BG_FOCUS 64,64,70
#define UI_COLOR_RIM 128,128,128
#define UI_COLOR_LIGHT 255, 255, 255
#define UI_COLOR_HILIGHT 240, 230, 210
#define UI_COLOR_MID 140, 130, 128

#define mouseOverlap(_x,_y,_w,_h) (mouseX() >= (_x) and mouseX() <= ((_x) + (_w)) and mouseY() >= (_y) and mouseY() <= ((_y) + (_h)))
#define toggle(x) (x xor 1)

type Synctrack
	// memblock data format
	// each entry is a datapoint as two float vars
	// offset	length	desc
	// 0		4		the point time as bars, 32-bit float
	// 4		4		the points value, a float between 0-1 (inclusive)
	field mem	// memblock
	field count
	field name as string
endType

type Envpoint
	field bars as float
	field value as float
	field index
endType

#ifdef USE_EDITOR_COMPONENTS
type EnvEditor
	field selectedPoint // just an id
	field deletePoint	// we'll delete the point usually ON the next frame
	field x
	field y
	field width
	field height
	field startpos#
	field endpos#
	field track
	field hover
	field snapsize as float
	field gridsize as float
	field timelineHeight 
endType
#endif

//#pragma dumb_debug

#ifdef STANDALONE_EDITOR

const TRACK_AMOUNT = 3

dim tracks(TRACK_AMOUNT)

SCREEN 800, 600
frameLimit 60

font = loadFont("tahoma", 14)
setFont font

for i=0 to TRACK_AMOUNT
	tracks(i) = createSyncTrack("track #" + i )
	
	point.Envpoint = new (Envpoint)	// only a temporary var
	
	for u=0 to 9
		point\bars = float(u)
		point\value = rnd(0, 1)
		track.Synctrack = toType(tracks(i))
		addEnvelopePoint(tracks(i), toInt(point), track\count)
	next u
	
	delete point
next i

for i=0 to TRACK_AMOUNT
	it.Synctrack = tracks(i)
	
	//addText "" + it\name + " count: " + it\count
	point.Envpoint = getEnvpoint(toInt(it), 0)
	//addText "" + point\bars + ", " + point\value
	delete point
	
	//addText "" + memDumpFloat(it\mem)
next i

editorp = createEnvEditor(0, 300, 800, 200, 0.0, 10.0)
editor.EnvEditor = toType(editorp)
editor\track = tracks(0)

tempo# = 130.0
tstart=timer()
pause_start=timer()
pause_offset# = 0.0
time_offset# = 0.0
bar_offset# = 0.0

playing = true
old_t# = 0.0
t# = 0.0
bars# = 0.0

repeat
	gosub update_time

	bars = updateEditor(toInt(editor), bars) // the editor processes looping
	newbars# = drawEditorTimeline(toInt(editor), bars)
	
	if newbars <> bars then
		diff# = newbars - bars
		bar_offset = bar_offset + diff
		time_offset = barsToSecs(bar_offset, tempo)
	endif
	//drawTrack(tracks(0), 0, 300, 800, 200, 0.0, 10.0) 
	
	drawEditor(toInt(editor), bars)
	
	value# = getEnvelopeValue(tracks(0), bars)
	
	if keyHit(cbKeyF6) then saveTrack(tracks(0), "test.dat")
	if keyHit(cbKeyF9) then 
		load_result = loadTrack(tracks(0), "test.dat")
		
		if load_result <= 0 then makeError "couldn't load track. error: "+ load_result
	endif
	
	addText "bars: " + bars + " bar offset " + bar_offset
	addText "playing: " + playing
	addText "value: " + value
	
	drawScreen
	clearText
forever

update_time:
	if keyHit(cbKeyBackspace) then
		time_offset = 0.0
		pause_offset = 0.0
		pause_start = timer()
		tstart = timer()
	endif
	
	if keyHit(cbKeySpace) then 
		if playing then
			playing = false
			pause_start = timer()
		else
			time_offset = time_offset + pause_offset
			pause_offset = 0.0
			playing = true
		endif
	endif
	
	if playing then
		old_t# = t
	else
		pause_offset# = -(timer()-pause_start)/1000.0
	endif
	
	t# = pause_offset + time_offset + float(timer() - tstart)/1000.0
	
	beats# = (t/60.0)*(tempo)
	bars# = beats/4.0 
return 

#endif



function loadTrack(trackp, filename$)
	track.Synctrack = toType(trackp)
	
	bytes_read = 0
	offset = 0
	file_size = fileSize(filename)
	
	//makeError "" + file_size

	//if file_size <= 0 then return -1
	
	memblock = malloc(file_size)
	
	if not fileExists (filename) then return bytes_read
	
	f = openToRead(filename)
	
	while not EOF(f)
		char = readByte(f)
		pokeByte memblock, offset, char
		offset = offset + 1
		//bars# = readFloat(f)
		//value# = readFloat(f)
	wend
	
	bytes_read = fileOffset(f)
	closeFile(f)

	free track\mem
	track\count = (bytes_read/8)
	track\mem = memblock

	return bytes_read
endFunction

#ifdef USE_EDITOR_COMPONENTS
// returns the amount of bytes written
function saveTrack(trackp, filename$)
	track.Synctrack = toType(trackp)

	f = openToWrite(filename)
	
	for i=0 to track\count-1
		offset = i * POINT_STRIDE
		writeFloat f, peekFloat(track\mem, offset)
		writeFloat f, peekFloat(track\mem, offset + 4)
	next i
	
	written = fileOffset(f)
	closeFile(f)
	
	return written
endFunction


function createEnvEditor(x,y,w,h,startspan,endspan)
	editor.EnvEditor = new(EnvEditor)
	editor\x = x
	editor\y = y
	editor\width = w
	editor\height = h
	editor\startpos = startspan
	editor\endpos = endspan
	editor\track = UNDEFINED
	editor\selectedPoint = UNDEFINED 
	editor\hover = false
	editor\deletePoint = UNDEFINED 
	editor\snapsize = 0.5
	editor\gridsize = 1.0
	editor\timelineHeight = 20
	return toInt(editor)
endFunction

function updateEditor(editorp, bars#)
	editor.EnvEditor = toType(editorp)
	mousedz# = mouseMoveZ()
	scrollspeed#  = 0.7
	
	origpos# = editor\startpos

	if keyDown(cbKeyA) then	
		editor\startpos = max(editor\startpos - scrollspeed, 0.0)
		editor\endpos = editor\endpos - (origpos - editor\startpos)
	elseIf keyDown(cbKeyD) then
		editor\startpos = editor\startpos + scrollspeed
		editor\endpos = editor\endpos + scrollspeed
	endif
	
	span# = editor\endpos - editor\startpos
	middle# = editor\startpos + span*0.5
	zoom# = mousedz * 1.0

	if keyDown(cbKeyW) then zoom = 0.2
	if keyDown(cbKeyS) then zoom = -0.2
	
	editor\startpos = editor\startpos + zoom#
	editor\endpos = editor\endpos - zoom#
	
	if editor\endpos - editor\startpos < 1.0
		editor\endpos = editor\startpos + 1.0
	endif
	
	if editor\deletePoint <> UNDEFINED then
		// deleteEnvpoint(trackint, index)
		deleteEnvpoint(editor\track, editor\deletePoint)
		editor\deletePoint = UNDEFINED
	endif
	
	return bars
endFunction

function drawEditor(editorp, bars#)
	editor.EnvEditor = toType(editorp)
	mx# = mouseX()
	my# = mouseY()
	mousedz# = mouseMoveZ()
	
	span# = editor\endpos - editor\startpos
	hover = false
	thisfirst = true
	lastx# = 0
	lasty# = 0
	
	gridspacing# = editor\gridsize
	bar_width_in_px# = editor\width / span
	grid_lines = roundDown(span/gridspacing)
	grid_size# = gridspacing * bar_width_in_px
	grid_start_x = bar_width_in_px - (int(editor\startpos * bar_width_in_px) mod int(bar_width_in_px))
	
	track.Synctrack = toType(editor\track)
	
	if mouseOverlap(editor\x, editor\y, editor\width, editor\height) then
		hover = true
		editor\hover = true
	else
		editor\hover = false
	endif
	
	if keydown(cbKeyLAlt) then snap = false else snap = true
	snapsize# = editor\snapsize
	
	// render bg
	
	color UI_COLOR_BG
	
	box editor\x, editor\y, editor\width, editor\height, 1
	
	if hover then
		color UI_COLOR_RIM
	else
		color UI_COLOR_DIM
	endif
	box editor\x, editor\y, editor\width, editor\height, 0	
	
	if editor\track = UNDEFINED then 
		return false
	endif
	
	// render grid
	color UI_COLOR_BG_FOCUS
	for i=0 to grid_lines
		line_x = grid_start_x + i * grid_size
		line line_x, editor\y, line_x, editor\y + editor\height
	next i	
	
	// render time marker
	marker_x_pos = editor\x + (bars - editor\startpos) * bar_width_in_px
	if bars >= editor\startpos and bars <= editor\endpos then
		color UI_COLOR_HILIGHT
		line marker_x_pos, editor\y, marker_x_pos, editor\y + editor\height + editor\timelineHeight
		
	endif
	
	spointp = firstPointAfter(editor\track, editor\startpos)
	ipointp = spointp
	
	if ipointp = UNDEFINED then
		return false
	endif
	
	spoint.Envpoint = toType(spointp)
		
	// we take one to the left to only render also the point over to the left hand side of the SCREEN
	if spoint\index > 0 then
		spointp = getEnvPoint(editor\track, spoint\index - 1)
		ipointp = spointp
		spoint = toType(spointp)
	endif
	
	ipoint.Envpoint = spoint
	ind = ipoint\index
			
	// current mouse position
	newbars# = editor\startpos + ((mx-editor\x)/editor\width) * span
	newvalue# = 1.0 - ((my-editor\y)/editor\height) * 1.0
	
	if snap then
		newbars = snapToGrid(newbars, snapsize)
	endif
	
	// a new point!
	if mouseHit(2) then
		before_pointp = firstPointBefore(editor\track, newbars)
		if before_pointp = UNDEFINED then
			// TODO write error to log
		else
			before_point.EnvPoint = toType(before_pointp)
			temp_point.EnvPoint = new(EnvPoint)
			temp_point\bars = newbars
			temp_point\value = clamp(newvalue)
			
			addEnvelopePoint(editor\track, toInt(temp_point), before_point\index + 1)
			
			delete temp_point
			delete before_point
		endif
	endif
	
	// a flag used to detect when we want to lose focus
	clicked_on_point = false

	while (ind < track\count)
		pointhover = false
		delete_current_point = false
		px# = editor\x + ((ipoint\bars - editor\startpos)/span) * editor\width
		py# = editor\y + editor\height - ipoint\value * editor\height
		
		if not thisfirst then
			color UI_COLOR_MID
			line lastx, lasty, px, py
		endif
		
		// we have processed all visible points
		if px > editor\x + editor\width then
			exit
		endif
		
		if ind=editor\selectedPoint then
			pointhover = drawPoint(px, py, true)
		else
			pointhover = drawPoint(px, py, false)
		endif
			
		if pointhover and mouseHit(1) then 
			editor\selectedPoint = ind
			clicked_on_point = true
		endif
		
		if mouseUp(1) then editor\selectedPoint = UNDEFINED

		if ind=editor\selectedPoint and editor\selectedPoint <> UNDEFINED then	
		
			if newvalue > 1.0 then newvalue = 1.0
			if newvalue < 0.0 then newvalue = 0.0
			
			if ind = 0 then
				if newbars < 0.0 then newbars = 0.0
			else
				// not the first one, let's inspect neighbours
				lpointp = getEnvpoint(editor\track, ind-1)
				lpoint.EnvPoint = toType(lpointp)
				if newbars < lpoint\bars then newbars = lpoint\bars
				
				// the right neighbour
				if ind < track\count-1 then
					rpointp = getEnvpoint(editor\track, ind+1)
					rpoint.EnvPoint = toType(rpointp)
					if newbars > rpoint\bars then newbars = rpoint\bars
					delete rpoint
				endif
				
				delete lpoint
			endif

			oldbars# = ipoint\bars
			oldvalue# = ipoint\value
			// setup temporary type instance
			ipoint\bars = newbars
			ipoint\value = newvalue
			
			// actually save the value to the array
			setEnvpoint(editor\track, toInt(ipoint), ipoint\index)
			
			if keyDown(cbkeylcontrol) then
				moveAllPointsFrom(editor\track, ipoint\index+1, ipoint\bars - oldbars)
			endif
			
			color UI_COLOR_MID
			text px + 15, py + 15, "" + ipoint\bars + ", " + ipoint\value
			
			if keyHit(cbKeyDel) or (pointhover and mouseDown(1) and keyDown(cbKeyX)) then
				editor\deletePoint = ipoint\index
				editor\selectedPoint = UNDEFINED
			endif
		endif			
		
		thisfirst = false
		lastx = px
		lasty = py
		
		if ind+1 >= track\count then exit
		
		delete ipoint
		ipoint = getEnvpoint(editor\track, ind+1)
		ind = ipoint\index
	wend		
	
	delete ipoint
	
	color UI_COLOR_MID
	text editor\x + 4, editor\y + 4, editor\startpos + " - " + editor\endpos

endFunction

function drawEditorTimeline(editorp, bars#)
	editor.EnvEditor = toType(editorp)
	mx# = mouseX()
	my# = mouseY()
	mousedz# = mouseMoveZ()
	newbars# = bars
	
	span# = editor\endpos - editor\startpos
	
	tl_x = editor\x
	tl_y = editor\y + editor\height
	tl_w = editor\width
	tl_h = editor\timelineHeight
	
	hover = false
	
	if mouseOverlap(tl_x, tl_y, tl_w, tl_h) then
		hover = true
	endif

	if hover and mouseDown(1) and editor\selectedPoint = UNDEFINED then
		newpos# = editor\startpos + ((mx-editor\x)/editor\width) * span
		newbars = newpos
	endif
	
	// bg box
	color UI_COLOR_BG
	box tl_x, tl_y, tl_w, tl_h
	
	// mouse marker
	if hover then
		color UI_COLOR_RIM
		line mx, tl_y, mx, tl_y + tl_h
	endif
	
	// draw the numbers
	gridspacing# = 1.0
	bar_width_in_px# = editor\width / span
	grid_lines = roundDown(span/gridspacing)
	grid_size# = gridspacing * bar_width_in_px
	grid_start_x = bar_width_in_px - (int(editor\startpos * bar_width_in_px) mod int(bar_width_in_px))
	
	for i=-1 to grid_lines step 1
		num_x = grid_start_x + i * grid_size
		num =  roundDown(editor\startpos) + int(i*gridspacing) + 1
		color UI_COLOR_DARK
		text num_x, tl_y + 2, num
		color UI_COLOR_MID
		text num_x, tl_y + 1, num
		//line line_x, editor\y, line_x, editor\y + editor\height
	next i	
	
	return newbars
endFunction

function drawPoint(x,y,selected)
	hover = false
	
	margin = 3
	if mouseOverlap(x-margin, y-margin, UI_POINT_SIZE + margin, UI_POINT_SIZE + margin) then
		hover = true
	endif

	if hover or selected
		color UI_COLOR_RIM
		box x - UI_POINT_SIZE/2-2, y - UI_POINT_SIZE/2-2, 8 + 4, 8 + 4, OFF
	endif

	if selected = 0 then
		color UI_COLOR_RIM
	elseIf selected = 1
		color UI_COLOR_LIGHT
	endif
	
	
	
	box x - UI_POINT_SIZE/2, y - UI_POINT_SIZE/2, 8, 8
	
	return hover
endFunction

function moveAllPointsFrom(trackp, index, amount#)
	track.Synctrack = toType(trackp)
	if index >= track\count then
		return false
	endif
	
	ind = index
	while (ind < track\count)
		// setEnvpointValue(trackint, newbars#, newvalue#, index)
		pointp = getEnvpoint(trackp, ind)
		point.EnvPoint = toType(pointp)
		point\bars = point\bars + amount
		setEnvpoint(trackp, pointp, ind)
		delete point
		ind + 1
	wend
	
	return true
endFunction


// index tells the place where to insert the new point
// returns void
function addEnvelopePoint(trackint, envint, index)
	track.Synctrack = toType(trackint)
	point.Envpoint = toType(envint)
	
	offset = index * POINT_STRIDE
	movesize = (MEMBlockSize(track\mem))-offset
	
	realloc track\mem, MEMBlockSize(track\mem) + POINT_STRIDE
	
	assert(index <= track\count)
	
	if movesize > 0 then
		// move rest of the memblock further to make room for the new var
		memCopy track\mem, offset, track\mem, offset + POINT_STRIDE, movesize
	endif

	pokeFloat track\mem, offset		, point\bars 
	pokeFloat track\mem, offset + 4	, point\value 
	
	track\count = track\count + 1
	//temp = allocEnvpoint(envint)
	//memCopy temp, 0, track\mem, track\count * 8, 2*4
	//free temp
endFunction

function deleteEnvpoint(trackint, index)
	track.Synctrack = toType(trackint)
	assert(index < track\count)
	
	if track\count = 1 then
		return false
	endif
	
	offset = index * POINT_STRIDE
	movesize = (MEMBlockSize(track\mem))-offset
	if movesize > 0 then
		memCopy track\mem, offset + POINT_STRIDE, track\mem, offset, movesize
	endif
	
	realloc track\mem, MEMBlockSize(track\mem) - POINT_STRIDE
	
	track\count = track\count - 1
endFunction

// returns void
function setEnvpoint(trackint, pointint, index)
	track.Synctrack = toType(trackint)
	assert(index < track\count)
	
	offset = index * POINT_STRIDE
	point.Envpoint = toType(pointint)
	pokeFloat track\mem, offset		, point\bars
	pokeFloat track\mem, offset + 4	, point\value
endFunction

function setEnvpointValue(trackint, newbars#, newvalue#, index)
	track.Synctrack = toType(trackint)
	assert(index < track\count)
	offset = index * POINT_STRIDE
	pokeFloat track\mem, offset		, newbars
	pokeFloat track\mem, offset + 4	, newvalue
endFunction
// returns a string representation of the memory block contents
function memDumpFloat(memblock)
	out$ = ""
	
	i = 0
	while i < MEMBlockSize(memblock)
		if i mod 4 = 0 then
			out = out + str(peekFloat(memblock, i))
			out = out + ", "
		endif
		i + 4
	wend
	
	return out
endFunction

#endif

// the bare core components start here

// returns a temporary memblock, remember to free it!
function allocEnvelopePoint(bars#, value#)
	tempblock = malloc(POINT_STRIDE)
	pokeFloat tempblock, 0, bars
	pokeFloat tempblock, 4, value
	return tempblock
endFunction

// returns an envpoint in a temporary type instance, remember to delete it
function getEnvpoint(trackint, index)
	track.Synctrack = toType(trackint)
	assert(index < track\count)
	assert(index >= 0)
	
	point.Envpoint = new(Envpoint)
	point\bars	= peekFloat(track\mem, index*POINT_STRIDE)
	point\value = peekFloat(track\mem, index*POINT_STRIDE + 4)
	point\index = index
	
	return toInt(point)
endFunction


function firstPointAfter(trackint, startbar#)
	track.Synctrack = toType(trackint)
	
	// linear search
	for i=0 to track\count-1
		point.Envpoint = getEnvpoint(trackint, i)
		if point\bars >= startbar then
			return toInt(point)
		endif
		
		delete point
	next i
	
	return UNDEFINED
endFunction


function firstPointBefore(trackint, startbar#)
	track.Synctrack = toType(trackint)
	
	// linear search
	for i=track\count-1 to 0 step -1
		point.Envpoint = getEnvpoint(trackint, i)
		if point\bars <= startbar then
			return toInt(point)
		endif
		
		delete point
	next i
	
	return UNDEFINED
endFunction

// returns a pointer to a created Synctrack type instance
function createSyncTrack(name$)
	track.Synctrack = new (Synctrack)
	track\name = name
	track\count = 1
	//track\mem = allocEnvelopePoint(0.0, 0.0)
	track\mem = malloc(4*2)
	pokeFloat track\mem, 0, 0.0
	pokeFloat track\mem, 4, 0.0
	
	return toInt(track)
endFunction

function getEnvelopeValue(trackp, bars#)
	track.Synctrack = toType(trackp)
	
	firstpoint.EnvPoint = toType(getEnvPoint(trackp, 0))
	lastpoint.EnvPoint = toType(getEnvPoint(trackp, track\count-1))
	
	if bars < firstpoint\bars then
		return firstpoint\value
	endif
	
	if bars > lastpoint\bars then
		return lastpoint\value
	endif

	// is this redundant?
	if track\count = 1 then
		return firstpoint\value
	endif
	
	pointp = firstPointBefore(trackp, bars)
	
	if pointp <> UNDEFINED then
		point.EnvPoint = toType(pointp)

		if point\index = track\count-1 then
			return point\value
		endif
		
		nextPoint.EnvPoint = toType(getEnvPoint(trackp, point\index + 1))
		value_diff# = nextPoint\value - point\value
		time_diff# = nextPoint\bars - point\bars
		
		progress# = (bars - point\bars) / time_diff#
		return lerp(point\value, nextPoint\value, progress)
	endif
	
	return 0.0
endFunction